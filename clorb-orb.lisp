;;;; The ORB Interface

(in-package :clorb)

(defvar *the-orb* nil)

(defvar *orb-initializers* nil)



;;;  interface ORB {				// PIDL

(define-corba-class ORB ()
  :slots 
  ((adaptor :initform nil :accessor adaptor)
   (active  :initarg :active  :accessor orb-active)
   (host    :initarg :host    :accessor orb-host)
   (port    :initarg :port    :accessor orb-port)
   (initial-references :initform '()
                       :accessor orb-initial-references)))

;; initial ref:
;;  ( name  .  ( string . object  ))

(defun set-initial-reference (orb name string &optional object)
  (let ((old (assoc name (orb-initial-references orb)
                    :test #'string=)))
    (cond (old
           (setf (car (cdr old)) string
                 (cdr (cdr old)) object))
          (t
           (push (cons name (cons string object))
                 (orb-initial-references orb))))))

(defun refresh-initial-references (orb)
  (dolist (ref (orb-initial-references orb))
    (when (cadr ref) (setf (cddr ref) nil))))


;;;    exception InvalidName {}; 
(define-user-exception CORBA:ORB/InvalidName
    :id "IDL:omg.org/CORBA/ORB/InvalidName:1.0")


(defun ORB_init (&optional args (orbid ""))
  (declare (ignore args orbid))
  (unless *the-orb*
    (setq *the-orb* (make-instance 'CORBA:ORB
                      :active t
                      :host *host*
                      :port *port*))
    (when *name-service*
      (set-initial-reference *the-orb* "NameService" *name-service*))
    (when *interface-repository*
      (set-initial-reference *the-orb* "InterfaceRepository"
                             *interface-repository*))
    (dolist (fn *orb-initializers*)
      (funcall fn *the-orb*)))
  (setf (orb-active *the-orb*) t)
  *the-orb*)

;;;    void shutdown( in boolean wait_for_completion );
(define-method shutdown ((orb orb) wait_for_completion)
  (setf (orb-active orb) nil))

(defvar *running-orb* t
  "Will be set to true in the process that is running the ORB server part.
If this is true, orb-wait will check server streams also.
Can be set to true globally for singel-process / development.")



;;;    Object string_to_object (in string str);
(define-method string_to_object ((orb orb) str) 
  (string-to-object orb str))

;;;    string object_to_string (in Object obj);
(define-method object_to_string ((orb orb) obj)
  (declare (ignore orb))
  (object-to-string obj))

(defun object-to-string (objref)
  (format nil
	  "IOR:铂О佝磲ъ轶＇殇孱糸豉磲蝮栳飙磲脲孱汜痼蹯狒轱灬礅溽ㄢ蹑驽颟磲蝮栳飙轱镡牝彐怩骀弪┅┅┅换镶赍泗射涕篝扉篝唛铋糸犰唧弪鲩沐īㄤ彐轭瀛礤翳镤扉篝唛铋糸犰唑彐弪孱沐è矧矧猢磲疸狎＇汜矧猸轭轸獒飙蝈驽蝈钽弩矧猢┅换镶赍泗蝈箫祧暹轭轸獒爝蝈驽蝈钽弩ㄩ镶赍泗射殇孱糸骈弪换蜥轶弩ㄉ铞犰殇吾礤┗ㄤ彐轭瀛礤翳镤蝈箫祧暹轭轸獒爝蝈驽蝈钽弩è矧矧猢钺礤戾è蝈姝孱趄ㄡ篌镢钺礤矧猸轭轸獒飙蝈驽蝈钽弩矧猢呼弩＇篝蜷铉僵┅躅戾篌蝈姝孱趄ㄥ蝌矧弦炉深鲠扉湮犴濠戾è镡ㄣ滗蝈姝孱趄┅躅戾篌镡戾è溴箝珙狒矧ㄣ徜蝈姝孱趄┅ㄣ镱è篝蜷铉溴箝珙狒矧箦翩镡镳后趄轭邕麸唢怅邈矧溴箝珙狒矧┅箦翩镡ㄦ躅汜祆溴箝珙狒矧矧猢┅┅箦翩ㄣ滗蝈姝孱趄镡戛镡戛┅换怙镬遽黠螂唣孱溟铉┗ㄤ彐轭瀛礤翳镤黠螂唣孱溟铉è矧矧猢换粕赝舂换鲲殇疱蜴矧磉黠螂┗ㄤ彐轭瀛礤翳镤疱蜴矧磉黠螂è矧矧猢戾è蝓铑轭绛矧猹舂矧猸麽轸┅换鲲殇蝓瞑┗ㄤ彐轭瀛礤翳镤蝓è矧矧猢戾è蝓铑轭绛矧猹舂祜镳麒殪矧猸徙糸鲥矧猢滹矧猸麽轸┅┅换郁狒躞泸遽翦哽轶轭祜铉泔躅衄换秕沃涕篝铄鬟扉篝┗换郁狒躞泸遽翦唢疱蜥糸镱哽轶轭橡弪狒轱钅彐镳弪换秕沃涕篝铄鬟扉篝┗换换郁狒躞珏暨溴驷蹯暨泔铘屮秕蔑铘屮泗┗换怙镬遽珏暨箦蝣殂暹轭骘蝽狒轱ㄩ渝蝣殂逶疱箦蝣殂暹豉疱换秕渝蝣殂迳铈矧磲糸镱箦蝣殂暹轭骘蝽狒轱┗换珏暨沲蝌孱溴痱邈狒邃镳弪狒轱箬秕熹铒忮躞邃怡铄泔溴换铄泔溴箬秕熹躞蝈箫祧暹轭轸獒爝蝈驽蝈钽镳弪狒轱轭篝遽换悯蝌孱珏暨沲蝌孱舁┗换溴痱邈狒珏暨沲蝌孱轭翳铄磲觑痱轭糸铉镦孟衣换换换豉疱溴篝蜷铉镶赍泗射换豉疱溴箦聃孱沐枷怅邈羯渚镶赍泗射涕篝换换换嗅蝮轭郁蜷铉殒殄镶赍泗义骝孱沐ㄤ彐躅篝蜷铉麸镡赍泗矧篝颟眭祠轲戾鲠祯瀛忾钿礤翳镤蝈篝箴扉舡躜篝颟ㄣ镱è铛祆礤翳镤ㄥ蝌矧⑸祆彗犰镡赍泗蝈驽蝈钽搴立篝颟è篝蜷铉羼踽礤翳镤泔蜮犰镢眭祠轲戾鲠祯瀛忾钿ㄡ滗蝮脲疳蝮瀛泔蜮犰镢蝈篝ㄣ镱è羼ㄣ狎徜潋螬候轵镳候弩镬鲥唛铋糸犰唑彐弪孱沐矧ㄤ邈镤瀛镡觌妁篝蜷铉脲┅戾è脲ㄤ邈镤瀛镡觌妁鲥泗矧脲┅痱秫磲脲轭篝犷沐孟衣梁序秫洪吼蝻骈戾Ж┅┅ㄤ镬轶ㄡ滗徜潋螬ㄡ篌弪ㄥ洪轱ㄣ狎徜潋┅戾è鲥蝮轱箦泔钿徜潋┅ㄨ矬翳轵徜潋┅痫螋ㄦ秕螋徜潋┅戾è痱镦殪磲脲殚镳痱镦殪忽弪箝镱鲥蝮轱鸿矬栾篝吼矧痫螋弘妁脲┅瘐箬痱镦殪镡赍泗痱镦殪弩痱秫┅┅痱秫┅┅换上液躜祗è篝蜷铉羼踽礤翳镤⑸弦躅磲蝮栳飙孱汜痼蹯狒轱ㄤ邈镤瀛桢篝蜷铉蝈篝＇躅磲蝮栳飙轱颟换骈戾函骘锆忉躜祗è篝蜷铉羼踽礤翳镤㈡殪澧戾è骈戾钺礤蝈篝┅麒孱篝蜷铉篝狎趔鏖翳蝈篝换栳栾篝疳螋殓铒蝈箦赳骈戾钺礤篚怏羼蝈篝痫箝糸镱＼蝈篝后翎螋博┅戾è轱颦篝蜷铉鏖翳镳孱骈戾ㄦ骈戾钺礤轰轵邈糸镱洪铕豸蝈徜扉铄骟┅┅篝蜷铉麸镡赍泗矧轱颦篝蜷铉┅┅ㄥ蝌矧⒄铗邈镧铋邃找礤翳镤立礤翳镤┅┅ㄤ彐躅箴扉舡躜篝颟戾è礤翳镤孱痫箝糸镱＼篝颟┅ㄩ礤翳镤孱鲠祯弩篚怏羼篝礤翳镤孱洎篚怏羼篝ǐ礤翳镤孱暴┅铋飑┅ㄤ彐躅篝蜷铉篝狎趔鏖翳篝蜷铉痱彐轼ㄡ钿窘戾铉翳篝蜷铉戾铉翳痱彐轼┅篝蜷铉篝蜷铉痱彐轼哄钿戾铉翳痱彐轼┅┅ㄤ彐躅疳蝮瀛泔蜮犰镢篝颟换篝硷怅哚滗蜻扉篝聚⒓脲唧趄轭缇换硷怅哚滗蜻扉篝奂镡赀徜潋⑤硷怅哚滗蚓换找弩汜疱洚戾è脲痫痫箝糸镱＼篝颟脲篝蜷铉┅麒孱脲痫箦赳脲篝蜷铉篚怏羼篝ǐ脲痫暴┅箦赳篝篚怏羼篝脲痫螬┅戾è徜潋扉篝Ж┅灬篝痫癌祜镳骘泔眄岘痫痫箝糸镱＼篝后翎螋灬篝痫螬麒殪泔眄岘痫滹瘐箬篚怏羼篝灬篝痫泔眄岘痫螬徜潋扉篝箦赳灬篝痫ǐ泔眄岘痫暴┅瘐箬篚怏羼篝灬篝痫螬徜潋扉篝鲠祯弩磲疸狎＇疳蝮瀛镡戥徜潋徜潋扉篝脲篝蜷铉┅┅ㄤ彐躅疳蝮瀛镡戥徜潋篝颟眭祠轲戾鲠祯瀛忾钿痱雉麸脲蝈篝箴扉舡躜篝颟ㄣ镱è铛祆痱雉麸脲瞟ㄥ蝌矧㈨犰骘蝽邃镡戥徜潋┅è篝蜷铉羼踽痱雉麸脲Ⅱ轵候轵è矧篝蜷铉羼踽痱雉麸脲㈤轱稷篝蜷铉羼踽痱雉麸脲┅换奸轱疬徜潋奂鲥蝮轱罹艰矬艟邰孩拣矧艟换艰矬艟奈迎篝戾蕊篝吾礤矧轲哚滗蝈篌换荐弪箝镱柬彡矧柬轭矧⒗戾è鲥蝮轱⒈阿痫螋⒉赴耿┅戾è痫痫箝糸镱＼蝈篝┅麒孱痫箦赳鲥蝮轱篚怏羼蝈篝痫螬箦赳蝈篝篚怏羼蝈篝ǐ痫暴┅┅戾è痫痫箝糸镱＼蝈篝┅麒孱痫箦赳痫螋篚怏羼蝈篝ǐ痫暴┅箦赳蝈篝篚怏羼蝈篝痫螬┅扉篝洪轱疳蝮瀛殚镳鲥蝮轱鲥蝮轱瞟蝈篝疳蝮瀛轭翦珏痫螋┅┅┅ㄤ彐躅溴泔溴桢篝蜷铉篝蜷铉ㄡ篌弪ㄥ鲥铕戾铉翳篝蜷铉┅戾è轭趔祜镳骘骝镯忮祜戾铉翳篝蜷铉怡泔祆邈疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍┅┅磲脲狎蜥戾铉翳轭趔洪铋糸犰泔铘孱趔轭趔哄戾礤铘豉疱Ж躅箝珙邃怡翦俯┅ㄤ彐躅溴泔溴镡觌妁篝蜷铉篝蜷铉鏖翳秕麴豸麸篝蜷铉秕舂祜镳鏖翳篝狒骘汨徙蝻篌篝蜷铉骘骝镯滹ㄥ汜箦篝狒òㄩㄥ耢汨＼ォ箦赳篝狒暴痱轭汨秕舂┅ū箦赳篝狒博痱轭ㄣ镤瀛汨狎疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍┅秕舂ú箦赳篝狒癌┅┅ㄤ彐躅溴泔溴镡觌妁鲥泗矧篝蜷铉戾è秕磲脲狎蜥蛋横潢躞翎忪烘殪飙痫轭翦哄戾礤铘豉疱孟衣梁香翦舂┅祜镳鏖翳篝狒骘汨徙蝻篌篝蜷铉骘骝镯滹ㄥ汜箦篝狒òㄩㄥ耢汨＼ォ箦赳篝狒暴鲥泗矧瘐箬屮翦钿ㄣ栳颦泔溴汨秕舂┅ū箦赳篝狒博鲥泗矧瘐箬屮翦钿疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍秕舂ú箦赳篝狒癌┅秕舂ㄤ彐躅疳蝮瀛殚镳鲥蝮轱篝颟眭祠轲戾鲠祯瀛忾钿磲觑痫螬疳蝮瀛轭翦珏篝宏躅氕犰祜麇舂ㄡ篌弪ㄥㄣ栳篝痫螬＼┅戾è黹铒疳蝮瀛轭翦珏篝后翎螋ǐ痫暴┅ㄣ镱磲觑黹铒颟┅