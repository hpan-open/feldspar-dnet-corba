;;;; clorb-orb.lisp -- The ORB Interface

(in-package :clorb)

(defvar *orb-class* 'clorb-orb
  "The class (name) of the ORB instantiated by CORBA:ORB_init.")

(defvar *the-orb* nil)

(defvar *orb-initializers* nil)

(defvar *running-orb* t
  "Will be set to true in the process that is running the ORB server part.
If this is true, orb-wait will check server streams also.
Can be set to true globally for singel-process / development.")


(deftype CORBA:ORBId ()
  'string)



;;;;  interface ORB {				// PIDL

(defclass CORBA:TYPECODEFACTORY ()
  ())

(defclass CORBA:ORB ()
  ())

(defclass clorb-orb (CORBA:ORB CORBA:TypeCodeFactory)
  ((adaptor :initform nil     :accessor adaptor)
   (active  :initarg :active  :accessor orb-active)
   (host    :initarg :host    :accessor orb-host)
   (port    :initarg :port    :accessor orb-port)
   (initial-references :initform '()
                       :accessor orb-initial-references)
   (default-initial-reference
    :initform nil
    :accessor orb-default-initial-reference)))



;;;; Internal orb interface


(defmethod the-orb ((orb CORBA:ORB))
  orb)


(defmethod create-client-request ((orb clorb-orb) &rest initargs)
  (apply #'make-instance 'client-request 
         :the-orb orb initargs))


(defmethod create-objref ((orb clorb-orb) &key ior-id expected-id 
                           raw-profiles profiles)
  (make-instance (find-proxy-class 
                  (if (equal ior-id "") expected-id ior-id))
    :id ior-id
    :profiles profiles
    :raw-profiles raw-profiles ))


;; initial ref:
;;  ( name  .  ( string . object  ))

(defun set-initial-reference (orb name string &optional object)
  (let ((old (assoc name (orb-initial-references orb)
                    :test #'string=)))
    (cond (old
           (setf (car (cdr old)) string
                 (cdr (cdr old)) object))
          (t
           (push (cons name (cons string object))
                 (orb-initial-references orb))))))

(defun refresh-initial-references (orb)
  (dolist (ref (orb-initial-references orb))
    (when (cadr ref) (setf (cddr ref) nil))))



;;; ORB Operations for interceptors

(defmethod has-received-exception ((orb clorb-orb) client-request)
  (declare (ignore client-request))
  nil)

(defmethod has-received-reply ((orb clorb-orb) client-request)
  (declare (ignore client-request))
  nil)

(defmethod will-send-request ((orb clorb-orb) client-request)
  (declare (ignore client-request))
  nil)

(defmethod has-received-request-header ((orb clorb-orb) server-request)
  (declare (ignore server-request))
  nil)

(defmethod has-received-request ((orb clorb-orb) server-request)
  (declare (ignore server-request))
  nil)

(defmethod will-send-exception ((orb clorb-orb) server-request)
  (declare (ignore server-request))
  nil)

(defmethod will-send-reply ((orb clorb-orb) server-request)
  (declare (ignore server-request))
  nil)

(defmethod will-send-other ((orb clorb-orb) server-request)
  (declare (ignore server-request))
  nil)


;;;; CORBA:ORB Interface

;;;    exception InvalidName {};
(define-user-exception CORBA:ORB/InvalidName
  :id "IDL:omg.org/CORBA/ORB/InvalidName:1.0")


;;;    void shutdown( in boolean wait_for_completion );
(define-method shutdown ((orb orb) wait_for_completion)
  ;;FIXME: wait_for_completion
  (declare (ignore wait_for_completion))
  (setf (orb-active orb) nil))


;;;    Object string_to_object (in string str);
(define-method string_to_object ((orb orb) str)
  (string-to-object orb str))

;;;    string object_to_string (in Object obj);
(define-method object_to_string ((orb orb) obj)
  (object-to-string obj))

(defun object-to-string (objref)
  (format nil
	  "IOR:铂О佝磲ъ轶＇殇孱糸豉磲蝮栳飙磲脲孱汜痼蹯狒轱灬礅溽ㄢ蹑驽颟磲蝮栳飙镡赍泗镡牝彐怩骀弪┅┅┅换镶赍泗射涕篝扉篝唛铋糸犰唧弪鲩沐īㄤ彐轭瀛礤翳镤扉篝唛铋糸犰唑彐弪孱沐è矧矧猢磲疸狎＇汜矧猸轭轸獒飙蝈驽蝈钽弩矧猢┅换镶赍泗蝈箫祧暹轭轸獒爝蝈驽蝈钽弩ㄩ镶赍泗射殇孱糸骈弪换蜥轶弩ㄉ铞犰殇吾礤┗ㄤ彐轭瀛礤翳镤蝈箫祧暹轭轸獒爝蝈驽蝈钽弩è矧矧猢钺礤戾è蝈姝孱趄ㄡ篌镢钺礤矧猸轭轸獒飙蝈驽蝈钽弩矧猢呼弩＇篝蜷铉僵┅躅戾篌蝈姝孱趄麒孱矧猸溴驷蹯舡轭轸獒飙蝈驽蝈钽矧猢箦赳蝈姝孱趄扉篝钺礤ㄦ矧磲铋怜立矧猸溴驷蹯舡轭轸獒飙蝈驽蝈钽矧猢钺礤┅瘐箬蝈姝孱趄矧猸轭轸獒飙蝈驽蝈钽弩矧猢┅躅戾篌蝈姝孱趄ㄥ蝌矧孟衣梁弦炉深鲠扉湮犴濠┅戾è镡ㄣ滗蝈姝孱趄┅躅戾篌镡戾è溴箝珙狒矧ㄣ徜蝈姝孱趄┅ㄣ镱è篝蜷铉溴箝珙狒矧箦翩镡镳后趄轭邕麸唢怅邈矧溴箝珙狒矧┅箦翩镡ㄦ躅汜祆溴箝珙狒矧矧猢┅┅箦翩ㄣ滗蝈姝孱趄镡戛镡戛┅换怙镬遽黠螂唣孱溟铉┗ㄤ彐轭瀛礤翳镤黠螂唣孱溟铉è矧矧猢换粕赝舂换鲲殇疱蜴矧磉黠螂┗ㄤ彐轭瀛礤翳镤疱蜴矧磉黠螂è矧矧猢戾è蝓铑轭绛矧猹舂矧猸麽轸┅换鲲殇蝓瞑┗ㄤ彐轭瀛礤翳镤蝓è矧矧猢躅戾篌蝓铑轭绛矧猹ㄣ弪蝻⒁躅犷麽⒂邋眢翳矧轶犰蝈徜蝓铑轭纰┅戾è镬蝓铑轭绛矧猹┅箦赳蝓铑轭绛矧猹铋飑躅鏖钿痱雉邈戾è蝓铑轭绛矧猹舂祜镳麒殪矧猸徙糸鲥矧猢滹矧猸麽轸┅箦赳蝓铑轭绛矧猹镬洎┅换郁狒躞泸遽翦哽轶轭祜铉泔躅衄换秕沃涕篝铄鬟扉篝┗换郁狒躞泸遽翦唢疱蜥糸镱哽轶轭橡弪狒轱钅彐镳弪换秕沃涕篝铄鬟扉篝┗ㄤ彐轭瀛礤翳镤泸遽翦唢疱蜥糸镱哽轶è矧孟衣梁弦漏镳溴姗磲ъ轶灬礅溽痄孟衣梁吾礤渲犰蹂侯犴镳侯犴痄横蜱蹴孱孟衣梁令横铢豉疱泔溴镳呼疱痄┅横蜱唔镤弩ㄥ汜箦镳喉镤痄ê疳蜥磉轭烈沁晌ê疳蜥磉秕烈沁险冤ê疳蜥磉轭秕烈沁晌险冤┅镳吼狎犴镳溴姗┅换郁狒躞珏暨溴驷蹯暨泔铘屮秕蔑铘屮泗┗换怙镬遽珏暨箦蝣殂暹轭骘蝽狒轱ㄩ渝蝣殂逶疱箦蝣殂暹豉疱换秕渝蝣殂迳铈矧磲糸镱箦蝣殂暹轭骘蝽狒轱┗换珏暨沲蝌孱溴痱邈狒邃镳弪狒轱箬秕熹铒忮躞邃怡铄泔溴换铄泔溴箬秕熹躞蝈箫祧暹轭轸獒爝蝈驽蝈钽镳弪狒轱轭篝遽换悯蝌孱珏暨沲蝌孱舁┗换溴痱邈狒珏暨沲蝌孱轭翳铄磲觑痱轭糸铉镦孟衣换换换豉疱溴篝蜷铉镶赍泗射换豉疱溴箦聃孱沐枷怅邈羯渚镶赍泗射涕篝换换鲲殇蝈玳篝弪唛铋糸犰唑彐弪孱沐ㄩ镶赍泗射殇轭镶赍泗镡戛换蜥轶弩ㄉ铞犰殇吾礤┗ㄤ彐轭瀛礤翳镤蝈玳篝弪唛铋糸犰唑彐弪孱沐è矧沆矧猸矧猢殇镡戛麒孱矧铒篝蜷铉殇┅ㄥ聃犰殇ㄡ篌镢殇矧猸轭轸獒飙蝈驽蝈钽弩矧猢呼弩＇篝蜷铉僵ㄥ蝌矧孟衣梁弦炉深鲠扉湮犴濠┅躅戾篌镡蜥轶瀛簌篝屙屮沐痿轱孟衣梁铝倪辛伊泊恒镯痨弭邃哳铹箦舡轭轸獒飙蝈驽蝈钽矧殇铋镡戛换换轴祯驷泗矧镳弪狒轱铙换轴祯迤徙麸蝙蝈玳篝弪喏犰蹂哝徙麸蝙换轭义痫箝麸蝙射殇换轭轴祯迤徙麸蝙驷泗矧ㄤ彐轭瀛礤翳镤蝈玳篝弪喏犰蹂哝徙麸蝙è矧沆矧猸矧猢殇驷泗矧ㄣ桢汶豉疱殇篝蜷铉ㄣ桢汶豉疱驷泗矧矧沆狍簌礅镬┅箦翩ㄧ弭栳箬殇鲠祯瀛驷泗矧蝈玳篝蝙驷泗矧┅换鲲殇躅蝈玳篝弪喏犰蹂哝徙麸蝙ㄩ义痫箝麸蝙射殇┗ㄤ彐轭瀛礤翳镤躅蝈玳篝弪喏犰蹂哝徙麸蝙è矧沆矧猸矧猢殇ㄣ桢汶豉疱殇篝蜷铉蝈龛狍殇鲠祯瀛驷泗矧蝈玳篝蝙┅换轴祯迤徙麸蝙祜镫躔喏犰蹂哝徙麸蝙ㄩ义痫箝麸蝙射殇┗ㄤ彐轭瀛礤翳镤祜镫躔喏犰蹂哝徙麸蝙è矧沆矧猸矧猢殇ㄣ桢汶豉疱殇篝蜷铉ㄧ弭栳箬殇鲠祯瀛驷泗矧蝈玳篝蝙┅换换深轸獒扉轭翳弦换弦孟衣梁弦逻轭轸ㄡ蜱哽轶衄矧忾洎ㄤ彐躅孟衣梁弦逻轭轸é镳糸镱犰狎珞矧忾箦舡轭轸麒孱ㄥ狎珞舂箦赳狎珞铋箦舡轭轸舂戾è轭骘铋飑ㄣ镱è铛祆翳瀛矧猹箦赳翳瀛矧猹磲脲轭篝犷沐矧猸沆狍螵横泗轹鸿矬栾篝吼矧痫螋┅箦赳箦舡轭轸舂箦赳轭骘ㄣ蝈狒瀛矧猸轭轸轭骘翳瀛矧猹狎珞矧忾洎ㄤ镬轶ㄦ矧猸轭轸獒扉弪螵镳吼蝈唛铋骖轭骘┅è铒豉疱翳瀛矧猹矧猸沆狍螵┅ㄣ栳铉瀛沆狍翳瀛矧猹矧猸沆狍螵┅麒孱箦舡轭轸麒孱钺礤箦蝣殂濯箦舡轭轸獒飙蝈驽蝈钽翳瀛矧猹⑽犴逵弪鲩沐钺礤箦蝣殂濯┅麒孱轭翦蜴徙瀛蝈痫箝麸蝙箦舡轭轸獒飙蝈驽蝈钽翳瀛矧猹⑸铘弪驷沐义痫箝麸蝙轭翦蜴徙瀛蝈痫箝麸蝙┅箦赳狎珞痱镢弩蟓矧猸狎珞翳瀛矧猹狎珞┅箦翩矧猸徙糸鲥翳瀛矧猹舂麒孱轭骘ㄤ镬轶ㄦ矧猸轭轸獒扉弪螵镳吼矬暨轭轸骖轭骘┅┅鲠祯弩翳瀛矧猹狎珞┅ㄤ彐礤翳镤泸遽翦矧猸轭轸轭骘è矧沆矧猸矧猢狎珞矧忾洎ㄤ邈灬蝈ㄩ珙矧狎珞矧忾洎矧猢ㄤ彐躅痱镢弩蟓矧猸狎珞矧狎珞戾è铄鳝狎珞Ж┅祜镳麒殪狎珞滹戾è狎痫狎珞┅ㄩ篝蜷铉篝狎趔鏖翳狎弦垄戾è鲠祯铋飑ㄦ戾è镳糸镱磲翥痱彐轼麒孱篝蜷铉篝狎趔鏖翳狎痱彐轼箦赳鲠祯篚怏羼狎戾铉翳痱彐轼┅箦赳鲠祯篝蜷铉戾骠趄轫鲠祯濠麒孱ㄥ聃犰鲠祯箦赳鲠祯痫狎珞┅舂┅ㄣ镱è镳糸镱磲翥弦律铋粢彐痱镢弩蟓镳舡轭轸獒飙蝈驽蝈钽矧鲠祯濠è镳糸镱磲翥弦履彐狨祠深轸义姊箦翩矧猸溴驷蹯舡轭轸獒飙蝈驽蝈钽矧猢鲠祯濠è镳糸镱磲翥弦滦矧簪箦翩矧猸痫螋矧猢疳蝮瀛狎绛轭翦珏鲠祯濠┅蜥轶瀛簌篝屙屮沐痿轱孟衣梁铝倪辛伊桐┅┅瘐箬狎铄鳝狎珞┅┅铗弼弪箦铄鳝狎珞┅｜孟衣梁弦逻轭轸Ж弦履彐狨祠深轸义姊泔蜮犰镢汉档碉怅螽泔恝┅孟衣梁弦逻轭轸Ж弦律铋粢彐⑽犴逵弪鲩沐姐矧忉祜愫罕怖祜汜扈矬艉舶氨吾礤渝蝣殂澧┅ㄤ彐躅痱镢弩蟓镳舡轭轸獒飙蝈驽蝈钽矧狎绌戾è羼痫痫箝糸镱＼狎绌┅躅戾篌羼痫ㄥ蝌矧⑸祆彗犰深轸獒煲彐弪孱沐镳糸镱立狎绌戾è钺礤篚怏羼狎羼痫螬ㄩ矧篚怏羼狎ǐ羼痫暴┅箦舡轭轸獒飙蝈驽蝈钽矧钺礤轱颟┅ㄤ彐躅疳蝮瀛狎绛轭翦珏ㄡ蜱豉疱汜箦狎铛礅弪狎绌篝蜷铉疳蝮瀛轭翦珏狎绌ㄥ蝌矧⒘蜱蹴孱箬秕熹忮轭翦珏蚝立狎绌┅换箩汶麽蜾泔眇狒殁扉豉鏖翳骢钽糸镱狍轭轸獒扉弪ㄤ彐轭瀛礤翳镤⑿遗呱紊寓è轭轸骢钽糸镱轭骘ㄦ躅汜祆轭轸翳瀛矧轭骘┅ㄤ彐轭瀛礤翳镤⑿嫌赃晌稍è轭轸骢钽糸镱轭骘ㄤ邈灬蝈ㄩ珙矧轭骘┅ㄤ彐轭瀛礤翳镤⑿遗呱紊寓è轭轸簌礅镬轭骘镳吼蝈唛铋簌礅镬骢钽糸镱轭轸轭骘┅ㄤ彐轭瀛礤翳镤⑿嫌赃晌稍è轭轸簌礅镬轭骘镳吼矬暨轭轸簌礅镬骢钽糸镱轭轸轭骘┅换换嗅蝮轭郁蜷铉殒殄镶赍泗义驽蝈钽弩ㄤ彐躅篝蜷铉麸镡赍泗矧篝颟眭祠轲戾鲠祯瀛忾钿礤翳镤蝈篝箴扉舡躜篝颟ㄣ镱è铛祆礤翳镤ㄥ蝌矧⑸祆彗犰镡赍泗蝈驽蝈钽搴立篝颟è篝蜷铉羼踽礤翳镤泔蜮犰镢ㄣ矧忉祜悱麸镡赍泗矧蝈篝┅è篝蜷铉羼踽礤翳镤泔蜮犷犴澧ㄣ矧忉钺礤麸镡赍泗矧蝈篝┅换上液躜祗è篝蜷铉羼踽礤翳镤⑸弦躅磲蝮栳飙孱汜痼蹯狒轱ㄤ邈镤瀛桢篝蜷铉蝈篝＇躅磲蝮栳飙镡赍泗┅换骈戾函骘锆忉躜祗è篝蜷铉羼踽礤翳镤㈡殪澧戾è疳翳钺礤疳蝮瀛骈戾躜蝈篝┅戾è轱颦篝蜷铉鏖翳镳孱骈戾ㄦ疳翳钺礤轰轵邈糸镱洪铕豸蝈徜扉铄骟┅┅篝蜷铉麸镡赍泗矧篝蜷铉趄轫．鲥泗矧＼羽徙＼涕铄驽邃＼义趱蝾轱颦篝蜷铉┅┅è篝蜷铉羼踽礤翳镤㈣趑稷眭祠轲戾鲠祯瀛忾钿ㄨ矬痫螋疳翳疳蝮瀛梏麴躜蝈篝戾è轱ㄨ趑瓠珏舡轱栾篝痫螋疳翳┅矧轱蜥轶瀛簌篝屙屮沐痿轱孟衣梁忉溥疳蜥愆篝蜷铉麸镡赍泗矧轱颟┅ㄥ蝌矧⒄铗邈镧铋邃找礤翳镤立礤翳镤┅┅ㄤ彐躅疳蝮瀛骈戾躜蝈篝戾è疳翳ㄩ篝蜷铉篝狎趔鏖翳蝈篝换栳栾篝疳螋殓铒蝈篚怏羼蝈篝痫箝糸镱＼蝈篝后翎螋博蝈篝┅戾è溟蝈泗矧Ж┅篝狎癌ㄣ镱è篝蜷铉篝狎趔鏖翳疳翳瘐箬横怏镬豸溟蝈泗矧ㄩ钽篝狎舂瘐箬候屐狒轹溟蝈泗矧┅祜镳骘痫痫箝糸镱＼疳翳后翎螋篝狎舂麒殪痫滹瘐箬篚怏羼疳翳篝狎痫螬溟蝈泗矧滹箦翩篝狎ū痫螬┅磲脲疳翳钺礤侯犴篚怏羼疳翳篝狎舂轰轵邈麸蝙铗弼弪箦溟蝈泗矧┅┅ㄤ彐躅疳蝮瀛梏麴躜蝈篝戾è栾篝㈧镢犰栾篝痫螋赴疳翳蝈篝┅麒孱篝蜷铉篝狎趔鏖翳蝈篝戾舄è疳翳孱戾铉翳蝈篝┅痫螋篝狎痫箝糸镱＼蝈篝后翎螋博痫螋孱矧痫箝糸镱＼蝈篝后翎螋博疳翳孱洎ㄨ矬舡孱矧痫螋篝狎痫螋孱洎┅箦赳栾篝篚怏羼蝈篝栾篝孱洎麒孱痫螋篝狎箦赳痫螋疳蝮瀛轭翦珏蝈篝后翎螋ū痫螋篝狎舂哄钿痫螋孱洎┅箦赳疳翳篚怏羼蝈篝痫螋孱洎┅鲠祯弩栾篝痫螋疳翳┅ㄤ彐躅泔蜮犰镢麸镡赍泗矧蝈篝镳糸镱犰溴驷蹯舡脲眭祠轲戾鲠祯瀛忾钿ㄡ滗蝮脲疳蝮瀛泔蜮犰镢蝈篝ㄩㄡ钿溴驷蹯舡脲ㄥ聃犰脲┅箦赳脲溴驷蹯舡脲┅ㄣ镱è羼ㄣ狎徜潋螬候轵镳候弩镬鲥唛铋糸犰唑彐弪孱沐矧ㄤ邈镤瀛镡觌妁篝蜷铉脲┅戾è脲ㄤ邈镤瀛镡觌妁鲥泗矧脲┅痱秫磲脲轭篝犷沐孟衣梁序秫洪吼蝻骈戾Ж┅┅ㄤ镬轶ㄡ滗徜潋螬ㄡ篌弪ㄥ洪轱ㄣ狎徜潋┅戾è鲥蝮轱箦泔钿徜潋┅ㄨ矬翳轵徜潋┅痫螋ㄦ秕螋徜潋┅戾è痱镦殪磲脲殚镳痱镦殪忽弪箝镱鲥蝮轱鸿矬栾篝吼矧痫螋弘妁脲┅瘐箬痱镦殪镡赍泗痱镦殪弩痱秫┅┅痱秫┅┅ㄤ彐躅泔蜮犷犴瀛麸镡赍泗矧蝈篝戾è钺礤痫痫箝糸镱＼蝈篝┅钺礤疳螋┅麒孱钺礤痫箦赳钺礤疳螋ㄤ邈镤瀛镡觌妁篝蜷铉篚怏羼蝈篝ū钺礤痫螬┅箦赳蝈篝篚怏羼蝈篝钺礤痫螬┅戾è钺礤泔铘屮ㄣ矧忉祜悱麸镡赍泗矧蝈篝⑽犴逵弪鲩沐┅矧猸蝈箫祧矧钺礤泔铘屮钺礤疳螋┅┅ㄤ彐躅箴扉舡躜篝颟戾è礤翳镤孱痫箝糸镱＼篝颟┅ㄩ礤翳镤孱鲠祯弩篚怏羼篝礤翳镤孱洎篚怏羼篝ǐ礤翳镤孱暴┅铋飑┅ㄤ彐躅篝蜷铉篝狎趔鏖翳篝蜷铉痱彐轼ㄡ钿窘戾铉翳篝蜷铉戾铉翳痱彐轼┅篝蜷铉篝蜷铉痱彐轼哄钿戾铉翳痱彐轼┅┅ㄤ彐躅疳蝮瀛泔蜮犰镢篝颟换篝硷怅哚滗蜻扉篝聚⒓脲唧趄轭缇换硷怅哚滗蜻扉篝奂镡赀徜潋⑤硷怅哚滗蚓换找弩汜疱洚戾è脲痫痫箝糸镱＼篝颟脲篝蜷铉┅麒孱脲痫箦赳脲篝蜷铉篚怏羼篝ǐ脲痫暴┅箦赳篝篚怏羼篝脲痫螬┅戾è徜潋扉篝Ж┅灬篝痫癌祜镳骘泔眄岘痫痫箝糸镱＼篝后翎螋灬篝痫螬麒殪泔眄岘痫滹瘐箬篚怏羼篝灬篝痫泔眄岘痫螬徜潋扉篝箦赳灬篝痫ǐ泔眄岘痫暴┅瘐箬篚怏羼篝灬篝痫螬徜潋扉篝鲠祯弩磲疸狎＇疳蝮瀛镡戥徜潋徜潋扉篝脲篝蜷铉┅┅ㄤ彐躅疳蝮瀛镡戥徜潋篝颟眭祠轲戾鲠祯瀛忾钿痱雉麸脲蝈篝箴扉舡躜篝颟ㄣ镱è铛祆痱雉麸脲瞟ㄥ蝌矧㈨犰骘蝽邃镡戥徜潋┅è篝蜷铉羼踽痱雉麸脲Ⅱ轵候轵è矧篝蜷铉羼踽痱雉麸脲㈤轱稷篝蜷铉羼踽痱雉麸脲┅换奸轱疬徜潋奂鲥蝮轱罹艰矬艟邰孩拣矧艟换艰矬艟奈迎篝戾蕊篝吾礤矧轲哚滗蝈篌换荐弪箝镱柬彡矧柬轭矧⒗戾è鲥蝮轱⒈阿痫螋⒉赴耿┅戾è痫痫箝糸镱＼蝈篝┅麒孱痫箦赳鲥蝮轱篚怏羼蝈篝痫螬箦赳蝈篝篚怏羼蝈篝ǐ痫暴┅┅戾è痫痫箝糸镱＼蝈篝┅麒孱痫箦赳痫螋篚怏羼蝈篝ǐ痫暴┅箦赳蝈篝篚怏羼蝈篝痫螬┅扉篝洪轱疳蝮瀛殚镳鲥蝮轱鲥蝮轱瞟ㄩㄥ聃犰蝈篝㈧镢犰栾篝蝈篝疳蝮瀛轭翦珏痫螋┅┅┅ㄤ彐躅溴泔溴桢篝蜷铉篝蜷铉ㄤ邈灬蝈篝蜷铉篝蜷铉┅ㄡ篌弪ㄥ鲥铕戾铉翳篝蜷铉┅戾è轭趔祜镳骘骝镯忮祜戾铉翳篝蜷铉怡泔祆邈疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍┅┅磲脲狎蜥戾铉翳轭趔洪铋糸犰泔铘孱趔轭趔哄戾礤铘豉疱Ж躅箝珙邃怡翦俯┅ㄤ彐躅溴泔溴镡觌妁篝蜷铉篝蜷铉ㄤ邈灬蝈篝蜷铉篝蜷铉┅鏖翳秕麴豸麸篝蜷铉秕舂祜镳鏖翳篝狒骘汨徙蝻篌篝蜷铉骘骝镯滹ㄥ汜箦篝狒òㄩㄥ耢汨＼ォ箦赳篝狒暴痱轭汨秕舂┅ū箦赳篝狒博痱轭ㄣ镤瀛汨狎疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍┅秕舂ú箦赳篝狒癌┅┅ㄤ彐躅溴泔溴镡觌妁鲥泗矧篝蜷铉ㄤ邈灬蝈篝蜷铉篝蜷铉┅戾è秕磲脲狎蜥蛋横潢躞翎忪烘殪飙痫轭翦哄戾礤铘豉疱孟衣梁香翦舂┅祜镳鏖翳篝狒骘汨徙蝻篌篝蜷铉骘骝镯滹ㄥ汜箦篝狒òㄩㄥ耢汨＼ォ箦赳篝狒暴鲥泗矧瘐箬屮翦钿ㄣ栳颦泔溴汨秕舂┅ū箦赳篝狒博鲥泗矧瘐箬屮翦钿疳蝮瀛轭翦珏篝蜷铉后翎螋哄钿ǐ博候徜轼倍秕舂ú箦赳篝狒癌┅秕舂ㄤ彐躅疳蝮瀛殚镳鲥蝮轱篝颟ㄤ邈灬蝈篝蜷铉篝颟眭祠轲戾鲠祯瀛忾钿磲觑痫螬疳蝮瀛轭翦珏篝宏躅氕犰祜麇舂ㄡ篌弪ㄥㄣ栳篝痫螬＼┅戾è黹铒疳蝮瀛轭翦珏篝后翎螋ǐ痫暴┅ㄣ镱磲觑黹铒颟┅换换孟衣梁好躜蝈铘ㄤ彐轭瀛泔蜮岘沆狍孟衣梁悯蝌孱ī＋矧呐粕闻晌耘移撩贤钱弦钳孟衣梁谜乙盼ㄏ率琶冤吼蝻ㄏ颓弦钳孟衣梁谜乙盼原幸县贤钱弦钳孟衣梁谜乙盼贤钱弦钳孟衣梁幸县侃洪⑸奶猴礴矧绡孟衣怜悯蝌孱艉碑阿侯犴⒚躜蝈铘换换酗扉泫呐粕闻撂闪贤钱弦钳孟衣梁邢躺觅再信洪⑸奶猴礴矧绡孟衣怜酗扉泫赠疱罕阿侯犴⑿镬殂赠疱呼疱贤钱弦钳孟衣梁仗衔呼疱泔溴贤钱弦钳孟衣梁悦哒滔吻呐粕闻晌耘移撩贤钱弦钳孟衣梁邢躺觅ㄏ率琶冤吼蝻ㄏ颓弦钳孟衣梁邢躺觅幸县贤钱弦钳孟衣梁邢躺觅贤钱弦钳孟衣梁幸县侃洪⑸奶猴礴矧绡孟衣怜酗扉泫罕阿侯犴⑿镬殂ㄤ彐轭瀛泔蜮岘沆狍痫扉泫轫痨ㄏ颓弦钳孟衣梁邢躺觅横趑蜷怩翦è痫扉泫唪疱候遽滹铎┅ㄤ彐轭瀛礤翳镤⒛庞砸腺è下痫扉泫轫痨┅ㄤ彐轭瀛礤翳镤⒚闲佗è下痫扉泫轫痨┅镡呐粕闻团匀夏⒛庞砸腺è下贤钱弦钳孟衣梁邢躺觅幸县侃ㄓ粤陨铆昧烫á溴篝蝻下施猴豸瘐è险孕赵┅洪铕豸è晌姓冤哄沐痿轱铙紊泰呐粕闻团匀夏⒚闲佗è下贤钱弦钳孟衣梁邢躺觅幸县侃ㄓ粤陨铆昧烫á泔瘗下施猴豸瘐è险孕赵┅洪铕豸è晌姓冤ㄕ瓮烈尤撂ㄓ偻孪汰再信孟呐贤钱弦钳孟衣梁邢躺觅晌姓冤哄沐痿轱铙紊泰呐粕闻团匀夏⑿咸擅龠再信è下贤钱弦钳孟衣梁邢躺觅幸县侃ㄇ旁猎砸陕赵下⑦珏暨痫扉泫唪疱ㄓ偻孪汰再信孟呐贤钱弦钳孟衣梁邢躺觅再信┅呐粕闻撂闪贤钱弦钳孟衣梁邢躺觅躺釉洪⑸奶猴礴矧绡孟衣怜酗扉泫涕篝罕阿侯犴⑿镬殂涕篝呼疱优颜盼门呼疱泔溴ㄣ蝈狒瀛箦聃孱沐翥ㄓ偻孪汰再信孟呐贤钱弦钳孟衣梁邢躺觅┅ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷箦沣镱篝蝓泗轱聃雉北┅ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷躅篚痧矧翦溥痫扉泫喏犰蹂聃雉穿ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷忉溥痫扉泫喏犰蹂聃雉畅ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷忉溥痫扉泫唪疱聃雉博ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷躅篚痧矧翦溥痫扉泫聃雉暴ㄤ彐泔铙翎铘镯绠矧绡泔蜮岷忉溥痫扉泫聃雉癌呐粕闻撂闪贤钱弦钳孟衣梁邢躺觅乓蚁颐夏洪⑸奶猴礴矧绡孟衣怜酗扉泫膨蝻蛎镤搴碑阿侯犴⑿镬殂膨蝻蛎镤澧呼疱贤钱弦钳孟衣梁尤弦呼疱泔溴贤钱弦钳孟衣梁悦哂认以呐粕闻沼乓咆门性上贤钱弦钳孟衣梁邢躺觅乓蚁洪⑸奶猴礴矧绡孟衣怜酗扉泫膨蝻蚝碑阿侯犴⑿镬殂膨蝻颌喉屙忮蝮èⅡ遽箫睥ㄓ偻孪汰再信孟呐贤钱弦钳孟衣梁邢躺觅乓蚁颐夏农┅ㄤ彐珏铄蜷泸遽翦痫扉泫豉疱鲠祯濠ㄤ彐轭瀛礤翳镤泸遽翦唣镬殂è矧弦漏豉疱鲠飑ㄣ蝈狒瀛痫扉泫豉疱ㄡ铢鲠祯鲠飑┅ㄤ彐礤翳镤泸遽翦痫扉泫è豉疱舂鲠祯濠ㄤ邈灬蝈ㄩ珙矧鲠祯濠ㄥ蝌矧ㄏ颓弦钳孟衣梁邢躺觅乓蚁候遽箫贤钱弦钳孟衣梁铝倪邢躺觅┅换换赠疱蔑溴漆泗矧换呐粕闻晌耘移撩孟衣梁再信孟呐屏迷弦ㄏ率琶冤换荷⑸奶猴礴矧绡孟衣怜赠疱蔑溴漆泗矧罕阿换何镣⒃疱蔑溴漆泗矧ㄤ彐轭瀛礤翳镤⒚遗猎胚遗谜矣芍胚悦è镡泔蜮岷豉疱泔溴驷泗矧殇磲脲豉疱泔溴候邈躜箝鲥殇┅ㄤ彐躅骈蝈沲蝮轹瀛翥翥戾è蝈沲颦扉篝Ж┅ㄩ镳洪翥┅灬忮祗è蝈镡戛豉疱汜箦镡箦聃孱沐磲铋＇蝈镡戛孟衣梁赠疱蔑溴ㄣ镱è羼镳弘轭镡戛候邈躜箝鲥麒孱ㄥ聃犰殇ㄦ轵篝豉疱泔溴疳蜥眢镡戛┅豉疱泔溴箜狍镡翥┅è铒礤礅弪镡蝈沲颦扉篝┅瘐箬镡蝈沲颦扉篝蝈豉疱泔溴疳蜥眢镡戛┅┅┅蝈翥翥┅ㄤ彐轭瀛礤翳镤⒚遗猎胚烈伊龠悦è镡泔蜮岷豉疱泔溴驷泗矧戾铉翳屐屙孱暨豉疱ㄣ蝈狒瀛狎蜥翥戾铉翳屐屙孱暨豉疱┅换腻痱邈狒邃换呐粕闻团匀夏⒚遗猎胚遗谜矣芍胚优颜盼门咴芒è镡泔蜮岷豉疱泔溴驷泗矧怙躅呦破优冤呐粕闻团匀夏⒚遗猎胚优颜盼门咴芒è镡泔蜮岷豉疱泔溴驷泗矧怙躅屐屙孱暨豉疱ㄣ蝈狒瀛箦聃孱沐翥怙躅屐屙孱暨豉疱┅ㄤ彐轭瀛礤翳镤⒚遗猎胚粕嘏倪悦è镡泔蜮岷豉疱泔溴驷泗矧溟玳趔筱犰濠ㄣ蝈狒瀛骈邃翥溟玳趔筱犰濠ㄤ彐轭瀛礤翳镤⒚遗猎胚子砸晌沁悦è下孟衣梁再信孟呐屏迷弦侃怙躅洎ㄣ蝈狒瀛黧趄轭绛翥怙躅洎ㄤ彐轭瀛礤翳镤⒚遗猎胚釉疑吻咴芒è下孟衣梁再信孟呐屏迷弦侃怙躅洎ㄣ蝈狒瀛篝蜷铉翥怙躅洎ㄤ彐轭瀛礤翳镤⒚遗猎胚晌耘移撩胚悦è下孟衣梁再信孟呐屏迷弦侃殇钺礤ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛轭翦蜴徙瀛翥殇钺礤┅ㄤ彐轭瀛礤翳镤⒚遗猎胚咆门性上芜悦è镡泔蜮岷豉疱泔溴驷泗矧殇钺礤礤礅弪螬ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛屮沐痿轱瞽翥殇钺礤箝眇戾篝蝓泗礤礅弪礤礅弪螬┅ㄤ彐轭瀛礤翳镤⒚遗猎胚釉艺迷咴芒è镡泔蜮岷豉疱泔溴驷泗矧殇钺礤礤礅弪螬ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛篝蝓泗翥殇钺礤箝眇戾篝蝓泗礤礅弪礤礅弪螬┅ㄤ彐躅箝眇戾篝蝓泗礤礅弪礤礅弪螬磲鲥泗矧灬礅溽愆扉篝镳侯犴愆镳呼疱愆┅礤礅弪螬ㄤ彐轭瀛礤翳镤⒚遗猎胚瘴上芜悦è镡泔蜮岷豉疱泔溴驷泗矧殇钺礤溟筱蜷黹钺麸颦豉疱礤礅弪螬ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛躅轱瞽翥殇钺礤溟筱蜷黹钺麸颦豉疱磲ъ轶灬礅溽礤礅弪戾舄è灬忮镳红徕屐礤礅弪┅灬忮飙鲠祯ㄡ铢鲠祯灬忮飑┅扉篝ㄩㄡ钿ㄥ呼脒镢翦ㄡ铢豉疱泔溴灬忮飑灬忮飙鲠祯濠т彐狨祠灬忮飙鲠祯濠镳侯犴礤礅弪镳呼疱礤礅弪┅┅礤礅弪螬┅ㄤ彐轭瀛礤翳镤⒚遗猎胚撂闪舆悦è镡泔蜮岷豉疱泔溴驷泗矧殇钺礤豉疱泔溴ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛犰獒蟓翥殇钺礤豉疱泔溴┅ㄤ彐轭瀛礤翳镤⒚遗猎胚盼胀咴芒è下孟衣梁再信孟呐屏迷弦侃殇钺礤礤礅弪螬ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛孱蹴翥殇钺礤礤礅弪螬┅ㄤ彐轭瀛礤翳镤⒚遗猎胚谅釉伊迷呱卧乓屏门咴芒è下孟衣梁再信孟呐屏迷弦侃殇钺礤ㄣ蝈狒瀛徕篝蜥泗轭翦蜴徙瀛翥殇钺礤┅ㄤ彐轭瀛礤翳镤⒚遗猎胚瘟陨峙咴芒è下孟衣梁再信孟呐屏迷弦侃殇钺礤ㄣ蝈狒瀛钺糸鲥翥殇钺礤┅ㄤ彐轭瀛礤翳镤⒚遗猎胚至陶胚孪剡悦è下孟衣梁再信孟呐屏迷弦侃殇钺礤怙邃唪疱ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛鲠祯瀛怙翥殇钺礤怙邃唪疱┅呐粕闻撂闪贤钱弦钳孟衣梁至陶磐夏善膳荷⑸奶猴礴矧绡孟衣怜轴祯逋镤殒殄蚝碑阿何镣⒅犰蹂惋溟骈弪涸傩贤钱弦钳孟衣梁尤弦涸傩琶夏贤钱弦钳孟衣梁悦哂认以ㄤ彐轭瀛礤翳镤⒚遗猎胚至陶胚悦è下孟衣梁再信孟呐屏迷弦侃殇钺礤豉疱唔镤殒殄泔钽蝈翦哜狍礤礅弪螬ㄦ轼蝈沲蝮轹瀛翥ㄣ蝈狒瀛鲠祯瀛翥殇钺礤豉疱唔镤殒殄泔钽蝈翦哜狍箝眇戾鲠祯瀛礤礅弪礤礅弪螬┅ㄤ彐躅箝眇戾鲠祯瀛礤礅弪礤礅弪螬磲鲥泗矧灬礅溽礤礅弪扉篝镳侯犴礤礅弪镳呼疱礤礅弪镳横沣弩礤礅弪┅礤礅弪螬ㄤ彐轭瀛礤翳镤⒚遗猎胚滔昧踢晌耘移撩胚悦è下孟衣梁再信孟呐屏迷弦侃殇钺礤ㄣ蝈狒瀛祜汜飙轭翦蜴徙瀛翥殇钺礤┅换换序秭殇轭徙沐篌麸翳矧换ㄤ彐礤翳镤翳瀛矧è蝈沆殄铘蝈聃弩舂换翳瀛矧猹ㄤ彐礤翳镤翳瀛矧è镡孟衣梁镶赍泗┅翳瀛矧猹ㄤ彐礤翳镤翳瀛矧è镡怩骀弪┅矧ㄢ蹑驽颦矧镡戛翳瀛矧猹┅